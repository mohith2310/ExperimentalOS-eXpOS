//--------------------------MEMORY MANAGER MODULE----------------//

alias funNum R1;

if(funNum == GET_FREE_PAGE)		then
	
	[SYSTEM_STATUS_TABLE+3] = [SYSTEM_STATUS_TABLE+3] +1;	//wait mem cnt

	//free mem cnt
	while([SYSTEM_STATUS_TABLE + 2] == 0)	do
		
		[PROCESS_TABLE + (16*[SYSTEM_STATUS_TABLE + 1]) + 4] = WAIT_MEM;
		
		backup;
		call MOD_5;
		restore;
	endwhile;

	[SYSTEM_STATUS_TABLE + 3] = [SYSTEM_STATUS_TABLE + 3] -1;	//wait mem cnt
	[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] -1;	//mem free cnt

	alias i R6;
	i = 0 ;

	while( i < 128)	do
		if([MEMORY_FREE_LIST + i] == 0)	then
			[MEMORY_FREE_LIST + i] = 1;
			break;
		endif;
		i = i+1;
	endwhile;

	R0 = i;

	return;
endif;

if(funNum == RELEASE_PAGE)	then
	alias pageNum R2;
	[MEMORY_FREE_LIST + pageNum] = [MEMORY_FREE_LIST + pageNum] -1;		
	if([MEMORY_FREE_LIST + pageNum] == 0)	then
		[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] + 1;	//mem free cnt

		alias i R7;
		i = 0;
		while(i < 16)	do
			if( [PROCESS_TABLE + (16*i) + 4] == WAIT_MEM)	then
				[PROCESS_TABLE + (16*i) + 4] = READY;
			endif;
			i = i + 1;
		endwhile;
	endif;
	return;
endif;

if(funNum == RELEASE_BLOCK)	then
	alias blockNum R2;
	[DISK_FREE_LIST + blockNum] = [DISK_FREE_LIST + blockNum] - 1;
	return;
endif;

if(funNum == GET_CODE_PAGE)	then

	alias blockNum R2;	//R2 contains block num
	alias i R3;
	alias block R4;
	alias pageNum R0;

	// IF WE GET A PAGE WHICH IS ALREADY PRESENT IN MEMORY	
	i = 0;
	while(i < 16) do
		
        block = 4;
		
        while(block < 8) do
			
            if( [DISK_MAP_TABLE + 10*i + block] == blockNum && [PAGE_TABLE_BASE + 20*i + 2*(block)] != -1) then
				
                pageNum = [PAGE_TABLE_BASE + 20*i + 2*block];
				[MEMORY_FREE_LIST + pageNum] = [MEMORY_FREE_LIST + pageNum] + 1;
				

                breakpoint;
                return;
			endif;
			
            block = block + 1;
		
        endwhile;		
	i = i+1;	
	endwhile;

	// IF NO FREE PAGE IS PRESENT AT MOMENT
	
	R1 = GET_FREE_PAGE;
	call MOD_2;                 //MEMORY_MANAGER

	R1 = DISK_LOAD;
	R4 = blockNum;
	R2 = [SYSTEM_STATUS_TABLE+1];
	R3 = R0;					// pageNum got from GET_FREE_PAGE
	call MOD_4;                 //DEVICE_MANGER


    breakpoint;
	
	return;
endif;
